apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "it.niedermann.nextcloud.deck"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1019003
        versionName "1.19.3"
        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    flavorDimensions "version"
    productFlavors {
        fdroid {
            dimension "version"
        }
        dev {
            dimension "version"
            applicationIdSuffix ".dev"
        }
        play {
            dimension "version"
            applicationIdSuffix ".play"
        }
        pfungstadt {
            dimension "version"
            applicationIdSuffix ".pfungstadt"
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }
}

dependencies {
    // Cross tab drag'n'drop
    implementation project(path: ':cross-tab-drag-and-drop')
    // TabLayoutHelper
    implementation project(path: ':tab-layout-helper')

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Android X
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.camera:camera-camera2:1.1.0-alpha07'
    implementation 'androidx.camera:camera-lifecycle:1.1.0-alpha07'
    implementation 'androidx.camera:camera-view:1.0.0-alpha28'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    def roomVersion = "2.3.0"
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.5.0'
    implementation "com.google.android.material:material:$rootProject.materialVersion"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    // Image compression
    implementation 'id.zelory:compressor:3.0.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'

    // Markdown
    implementation 'com.github.stefan-niedermann:nextcloud-notes:3.4.11'

    // Single-Sign-On
    implementation 'com.github.nextcloud:Android-SingleSignOn:0.5.6'
    implementation 'com.github.stefan-niedermann.nextcloud-commons:sso-glide:1.4.0'
    implementation 'com.github.stefan-niedermann.nextcloud-commons:exception:1.4.0'
    implementation 'com.github.stefan-niedermann.android-commons:util:0.2.0'
    implementation 'com.github.stefan-niedermann.android-commons:shared-preferences:0.2.0'

    // Custom Date / Time Picker for branding support
    implementation 'com.wdullaer:materialdatetimepicker:4.2.3'

    // Flexbox
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // Custom Color Picker
    implementation 'com.github.skydoves:colorpickerview:2.2.3'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.7'

    // Retrofit
    //noinspection GradleDependency
    implementation 'com.squareup.retrofit2:retrofit:2.6.4'

    // ReactiveX
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.6.1'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
}
