apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"
    defaultConfig {
        applicationId "it.niedermann.nextcloud.deck"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 1020000
        versionName "1.20.0"
        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    flavorDimensions "version"
    productFlavors {
        fdroid {
            dimension "version"
        }
        dev {
            dimension "version"
            applicationIdSuffix ".dev"
        }
        play {
            dimension "version"
            applicationIdSuffix ".play"
        }
        pfungstadt {
            dimension "version"
            applicationIdSuffix ".pfungstadt"
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }
    lint {
        abortOnError false
        disable 'MissingTranslation'
    }
}

dependencies {
    // Cross tab drag'n'drop
    implementation project(path: ':cross-tab-drag-and-drop')
    // TabLayoutHelper
    implementation project(path: ':tab-layout-helper')

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Android X
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.camera:camera-camera2:1.1.0-beta03'
    implementation 'androidx.camera:camera-lifecycle:1.1.0-beta03'
    implementation 'androidx.camera:camera-view:1.1.0-beta03'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.preference:preference:1.2.0'
    def roomVersion = "2.4.2"
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.7.1'
    implementation "com.google.android.material:material:$rootProject.materialVersion"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.13.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.1'

    // Image compression
    implementation 'id.zelory:compressor:3.0.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'

    // Single-Sign-On
    implementation 'com.github.nextcloud:Android-SingleSignOn:0.6.1'
    implementation 'com.github.stefan-niedermann.nextcloud-commons:sso-glide:1.6.2'
    implementation 'com.github.stefan-niedermann.nextcloud-commons:exception:1.6.2'
    implementation('com.github.stefan-niedermann.nextcloud-commons:markdown:1.6.3') {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }
    implementation 'com.github.stefan-niedermann.android-commons:util:0.2.6'
    implementation 'com.github.stefan-niedermann.android-commons:shared-preferences:0.2.6'

    // Custom Date / Time Picker for branding support
    implementation 'com.wdullaer:materialdatetimepicker:4.2.3'

    // Flexbox
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // Custom Color Picker
    implementation 'com.github.skydoves:colorpickerview:2.2.4'

    // Gson
    implementation 'com.google.code.gson:gson:2.9.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // ReactiveX
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'

    // Zoom Layout
    implementation("com.otaliastudios:zoomlayout:1.9.0")

    // Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.7.3'
    testImplementation 'org.mockito:mockito-core:4.4.0'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
}
